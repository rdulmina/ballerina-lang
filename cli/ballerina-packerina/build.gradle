/*
 * Copyright (c) 2019, WSO2 Inc. (http://www.wso2.org) All Rights Reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *
 */
 
apply from: "$rootDir/gradle/javaProject.gradle"

configurations {
    birJar
    distributionBirJar
    birJarCompile { transitive false }
    testCompile.exclude group: 'org.slf4j', module: 'slf4j-log4j12'
    testCompile.exclude group: 'org.slf4j', module: 'slf4j-simple'
    testCompile.exclude group: 'org.ops4j.pax.logging', module: 'pax-logging-api'
}

dependencies {

    distributionBirJar project(path: ':ballerina-io',  configuration: 'distributionBirJar')
    distributionBirJar project(path: ':testerina:testerina-core',  configuration: 'distributionBirJar')

    testCompile project(':ballerina-io')

    implementation project(':ballerina-lang')
    implementation project(':ballerina-config')
    implementation project(':ballerina-logging')
    implementation project(':ballerina-runtime')
    implementation project(':ballerina-tool')
    implementation project(':ballerina-tools-api')
    implementation project(':testerina:testerina-core')
    implementation project(':testerina:testerina-runtime')
    implementation project(':docerina')
    implementation project(':ballerina-cli-module')
    implementation project(':maven-resolver')
    implementation 'com.moandjiezana.toml:toml4j'
    implementation 'info.picocli:picocli'
    implementation 'org.apache.commons:commons-compress:1.18'

    testCompile 'org.testng:testng'
    testCompile 'com.moandjiezana.toml:toml4j'


}

task createBirCache {
    dependsOn configurations.distributionBirJar

    doLast {
        configurations.distributionBirJar.each { artifact ->
            copy {
                from artifact
                into "$buildDir/bir-cache/"
            }
        }
    }

    inputs.files configurations.distributionBirJar
    outputs.dir "$buildDir/bir-cache"
    outputs.cacheIf { false }
}

checkstyleMain {
    exclude 'org/ballerinalang/packerina/model/BaloToml.java'
}

test {
    dependsOn createBirCache
    dependsOn configurations.birJar
    dependsOn configurations.birJarCompile

    systemProperty "java.util.logging.manager", "org.ballerinalang.logging.BLogManager"
    useTestNG() {
        suites 'src/test/resources/testng.xml'
    }
    doFirst {
//        systemProperty "ballerina.bootstrap.home", files(configurations.birJarCompile).singleFile
    }
}

description = 'Ballerina - Packerina'

ext.moduleName = 'ballerina-packerina'

compileJava {
    inputs.property("moduleName", moduleName)
    doFirst {
        options.compilerArgs = [
                '--module-path', classpath.asPath,
        ]
        classpath = files()
    }
}
